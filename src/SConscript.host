Import('env')

import os
import re

from git_util import GitUtil

g = GitUtil(None)
m = re.match('v(\d+)\.(\d+)-(\d+)', g.describe())
SOFTWARE_VERSION = "%s.%s.%s" % (m.group(1), m.group(2), m.group(3))

host_defines = {'___SOFTWARE_VERSION___': '\\"%s\\"' % SOFTWARE_VERSION}
host_defines.update(env.get('CPPDEFINES', {}))

pyext = env.SharedLibrary('dmf_control_board_base',
                Glob('*.cpp'),
                CPPDEFINES=host_defines,
                SHLIBPREFIX='')
if os.name == 'nt':
    # In Windows, we must rename the compiled dll to have the extension 'pyd'.
    # Otherwise, it will not be recognizes as a Python extension.
    pyext = Command('dmf_control_board_base.pyd', pyext, [Copy("$TARGET", "$SOURCE")])

Export(pyext=pyext)
